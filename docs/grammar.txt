program = line, {line break, line}, [line break];
line = constant positive integer, [statement | comment];

statement =
	statement array definition
	| statement assign
	| statement jump
	| statement condition
	| function call;
statement array definition =
	"DIM", identifier, (
		"[", constant positive integer, "]"
		| "=", constant string
	);
statement assign = "LET", (identifier | array access), "=", expression addition;
statement jump = "GOTO", constant positive integer;
statement condition =
	"IF", expression addition, ("=" | "<" | ">"), expression addition,
	"THEN", constant positive integer;

expression addition =
	expression multiplication,
	{("+" | "-"), expression multiplication};
expression multiplication = expression atom, {("*" | "/"), expression atom};
expression atom =
	constant real
	| identifier
	| array access
	| function call
	| "(", expression addition, ")";

comment = "REM", {? /./ ? - line break};
identifier = ? /[a-z_]\w*/i ? - "DIM" - "LET" - "GOTO" - "IF" - "THEN" - "REM";
array access = identifier, "[", expression addition, "]";
function call =
	identifier, "(",
		[expression addition, {",", expression addition}],
	")";

line break = ? /(\r|\n)+/ ?;
constant positive integer = ? /[1-9]\d*/ ?;
constant real = ? /[1-9]\d*(\.\d+)?/ ?;
constant string = ? /"(\\.|[^"])*"/ ?;
